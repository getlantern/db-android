plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.google.protobuf' version '0.9.1'
}

android {
    compileSdk 34

    namespace 'io.lantern.db'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled = true
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }

    sourceSets {
        test {
            proto.srcDirs += 'src/test/protos'
            java.srcDirs += 'build/generated/source/proto/debugUnitTest/java' // this is a hack to get Android Studio to recognize the generated protocol buffer classes
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-collections-immutable-jvm:0.3.7'

    api group: 'net.zetetic', name: 'android-database-sqlcipher', version: '4.5.4'
    api "androidx.sqlite:sqlite-ktx:2.4.0"

    implementation ('com.esotericsoftware:kryo:5.6.0') {
        exclude group: "org.objenesis"
    }
    implementation 'org.objenesis:objenesis:3.4'

    implementation "com.google.protobuf:protobuf-javalite:$protoc_version"
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.11.0'

    implementation 'com.getkeepsafe.relinker:relinker:1.4.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protoc_version"
    }

    plugins {

    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}